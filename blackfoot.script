##########################################
#          Blackfoot Morphology          #
##########################################

define CONS [m|M|n|N|p|t|k|s|S|ts|ks|h|'] ;
define SEMI [w|y] ;
define VOW [a|i|o|á|í|ó|I] ; # no way to represent suprasegmentals?
define ACCENT [á|í|ó] ; # vowels with pitch accent
define NONPERM [I|M|N|S] ; # non-permanent segments

# ALLOMORPHY

define StemInitialNasalLoss [ m -> 0 || %^ _ ] ;
define ThirdPersonPrefixMutation [ w -> m || .#. _ {^a} ] ;  ! p.81
define PossessiveAllomorphs [ {^innaan} -> {^nnaan}, {^innoon} -> {^nnoon} || [i|a|w|y] _ ] ; # p.81 footnote 14

define Allomorphy StemInitialNasalLoss .o. ThirdPersonPrefixMutation .o. PossessiveAllomorphs ;

# PHONOLOGICAL RULES

define VowelShortening [ {aa} -> a, {ii} -> i, {oo} -> o, {áá} -> á, {íí} -> í, {óó} -> ó || _ %^ VOW ] ;

define NonPermanentConsonantLoss [ NONPERM -> 0 || _  {^i}|{^iksi}|{^istsi} .#. ] ;

define AccentSpread [ a -> á, i -> í, o -> ó || ACCENT %^ _ ] ;

define IgnoreMorphemeBoundary [ %^ -> 0 ] ;
define IgnoreNonPermanentConsonant [ M -> m, N -> n, S -> s ] ;

define tAffrication [ t -> ts || _ [i|í] ] ;

define Coalescence [ {wi} -> o, {wii} -> o ] ;

define SemivowelLoss [ SEMI -> 0 || [.#.|CONS] _ ] ; ! doesn't interact with VowelShortening/AccentSpread

define Phonology VowelShortening .o. NonPermanentConsonantLoss .o. AccentSpread .o. IgnoreMorphemeBoundary .o. IgnoreNonPermanentConsonant .o. Coalescence .o. tAffrication .o. SemivowelLoss ;

define Rules [ Allomorphy .o. Phonology ] ;

read lexc blackfoot.lexc
define Lexicon;

regex [ Lexicon .o. Rules ] ;