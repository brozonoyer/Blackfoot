##########################################
#          Blackfoot Morphology          #
##########################################

define CONS [m|M|n|N|p|t|k|s|S|h] ; # include ' in VOW instead of CONS
define NASAL [m|M|n|N] ;
define SEMI [w|y] ;
define VOW [a|i|o|á|í|ó|I|'] ; # no way to represent suprasegmentals?
define UNACCENTEDVOWEL [a|i|o|I] ;
define ACCENT [á|í|ó] ; # vowels with pitch accent
define NONPERM [I|M|N|S] ; # non-permanent segments

# ALLOMORPHY

define StemInitialNasalLoss [ NASAL -> 0 || %^ _ ] ;
define ThirdPersonPrefixMutation [ w -> m || .#. _ {^a} ] ;  ! p.81
define PossessiveAllomorphs [ {^innaan} -> {^nnaan}, {^innoon} -> {^nnoon} || [i|a|w|y] _ ] ; # p.81 footnote 14

define Allomorphy StemInitialNasalLoss .o. ThirdPersonPrefixMutation .o. PossessiveAllomorphs ;

# PHONOLOGICAL RULES

define SemivowelLoss [ SEMI -> 0 || .#.|[CONS %^] _ ] ;

define NonPermanentConsonantLoss [ NONPERM -> 0 || _ %^ VOW ] ;  ! see analysis in ch.15

define VowelShortening [ {aa} -> a, {ii} -> i, {oo} -> o, {áá} -> á, {íí} -> í, {óó} -> ó || _ %^ VOW ] ;

define AccentSpread [ a -> á, i -> í, o -> ó || ACCENT %^ _ ] ;  ! apply at morpheme boundaries to prevent /mohsóóa'tsiS/ from appearing as [mohsóóá'tsiS]

define IgnoreMorphemeBoundary [ %^ -> 0 ] ;
define IgnoreNonPermanentConsonant [ M -> m, N -> n, S -> s ] ;

define tAffrication [ t -> {ts} || _ %^ [i|í] ] ;

define Coalescence [ {w^i} -> o, {w^ii} -> o, {w^í} -> ó, {w^íí} -> ó ] ;

define Desyllabification [ i -> y, o -> w || UNACCENTEDVOWEL _ UNACCENTEDVOWEL ] ;  ! /oaawa/ -> [waawa] in nookóówawaawaiksi

define Phonology Coalescence .o. NonPermanentConsonantLoss .o. SemivowelLoss .o. VowelShortening .o. AccentSpread .o. tAffrication .o. IgnoreMorphemeBoundary .o. IgnoreNonPermanentConsonant.o. Desyllabification;

define Rules [ Allomorphy .o. Phonology ] ;

read lexc blackfoot.lexc
define Lexicon;

regex [ Lexicon .o. Rules ] ;
